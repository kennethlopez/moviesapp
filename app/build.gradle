apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.movies"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "${applicationId}.runner.AndroidJUnitRunner"

        project.ext.set("archivesBaseName", "movies_v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix " debug"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    final RETROFIT_VERSION = '2.6.1'
    final RXANDRIOD_VERSION = '2.1.1'
    final RXJAVA_VERSION = '2.2.12'
    final ROOM_VERSION = '2.2.0-rc01'
    final GLIDE_VERSION = '4.9.0'
    final BUTTERKNIFE_VERSION = '10.2.0'
    final DAGGER_VERSION = '2.11'

    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    // ui
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    // network
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:3.4.1"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:$RXANDRIOD_VERSION"
    implementation "io.reactivex.rxjava2:rxjava:$RXJAVA_VERSION"

    // local database
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION"

    // db test helper
    testImplementation "androidx.room:room-testing:$ROOM_VERSION"

    // media/image
    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    // view binding
    implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // injection
    implementation  "com.google.dagger:dagger:$DAGGER_VERSION"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2
    annotationProcessor daggerCompiler
    testAnnotationProcessor daggerCompiler
    androidTestAnnotationProcessor daggerCompiler

    // tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}
